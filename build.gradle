// Run at command line using 'gradle -q'

import groovy.json.*

defaultTasks 'build'

project.ext {
	libraryName = "ExtJS"
	libraryVersion = "4.2.0"

	// False to omit private methods/properties
	includePrivate = false

	// True to include full type values for properties, params, and return types.
	// Generates method overrides where necessary
	// WARNING: enabling types creates a definition with hundreds of compile errors due to incorrect docs or invalid scenarios in ExtJS itself
	// False to omit all types and set everything to "any"
	useFullTyping = true

	// Force all parameter types to "any" where possible
	forceAllParamsToOptional = true

	// False to generate separate definitions for each package
	singleDefinition = true
	currentModule = ""


	definitionPath = "./definition"
	definitionFile = new File( "${definitionPath}/${ libraryName }-${ libraryVersion }-${ useFullTyping ? 'Typed' : 'Untyped' }.d.ts" )

	// Handle one-off cases where ExtJS (or its JSDuck docs) create invalid scenarios...
	specialCases = [:]
	specialCases[ "Ext.grid.column.Action" ] = [:]
	specialCases[ "Ext.grid.column.Action" ][ "isDisabled" ] = "delete" // cfg item
	specialCases[ "Ext.AbstractComponent" ] = [:]
	specialCases[ "Ext.AbstractComponent" ][ "animate" ] = "toProperty" // method
	specialCases[ "Ext.util.Animate" ] = [:]
	specialCases[ "Ext.util.Animate" ][ "animate" ] = "toProperty" // method
}

// TASKS

/*
task wrapper( type: Wrapper ) {
	gradleVersion = '1.4'
}
*/


/*
Special cases:

In Ext.grid.column.(I)Action, omit isDisabled cfg item
In Ext.grid.(I)ActionColumn, omit isDisabled cfg item (alias for Ext.grid.column.Action)
In Ext.(I)AbstractComponent, turn animate() method into animate:any
In Ext.util.(I)Animate, turn animate() method into animate:any

Ext.zip(), delete, or don't allow spread because it has varargs first???
? Ext.create() has ( name, ...args ) so name can't be optional
? Ext.log() has ( name, ...args ) so name can't be optional

*/

task initialize() << {
	new File( definitionPath ).deleteDir()
	new File( definitionPath ).mkdir()

	println " "
	println "Creating ExtJS Definition File..."
	println " "

	if( project.singleDefinition ) {
		project.definitionFile.write( "" )
		writeToDefinition( "// ${ libraryName } ${ libraryVersion } TypeScript Library Definition\n" )
	}
}

task readFiles( dependsOn: initialize ) << {

	def jsonDocPath = new File( "./jsondocs" )
	def jsonDocCollection = files { jsonDocPath.listFiles() }
	def jsonDocFiles = jsonDocCollection.getFiles()

	jsonDocFiles.each { file ->
		def slurper = new JsonSlurper()
    	def fileJson = slurper.parseText( file.text )

		// Only handle classes, not JS primitives like String
		if( fileJson.name.contains( "Ext" ) ) {
			def aliases = getAliases( fileJson )
			println( "Processing ${ fileJson.name }" )
			processModule( fileJson.name, fileJson )

			// Also generate definitions for alternate class names, since crazy ExtJS swaps them out all over the docs...
			fileJson.alternateClassNames.each { thisAlias ->

				// Some aliases are absurdly the same as the original, but with different package case, so skip these
				if( thisAlias.toLowerCase() != fileJson.name.toLowerCase() )
					processModule( thisAlias, fileJson )
			}
		}
	}
}

def processModule( className, fileJson ) {
	project.currentModule = getModule( className )
	writeToDefinition( "declare module ${ getModule( className ) } {" )
	def processedNames

	// Ext class has special handling to turn it into Ext module-level properties and methods.
	if( className == "Ext" ) {
		processedNames = writeProperties( fileJson, false, true )
		writeMethods( fileJson, processedNames, false, true )
	}
	else {
		processClass( className, fileJson )
	}

	writeToDefinition( "}" )
}

def processClass( className, fileJson ) {
	def processedNames
	writeToDefinition( "\texport interface I${ getClassName( className ) } ${ getExtends( fileJson, true ) } {" )
	if( !fileJson.singleton ) {
		processedNames = writeProperties( fileJson, true, false )
		writeMethods( fileJson, processedNames, true, false )
	}
	writeToDefinition( "\t}" )

	writeToDefinition( "\texport class ${ getClassName( className ) } ${ getExtends( fileJson, false ) } ${getImplementedInterfaces( fileJson ) } {" )
	processedNames = writeProperties( fileJson, false, false )
	writeMethods( fileJson, processedNames, false, false )
	writeToDefinition( "\t}" )
}

def getExtends( fileJson, isInterface ) {
	// fileJson.superclasses.last() for extends
	def result = ""
	if( fileJson.superclasses?.size() > 0 ) {
		if( isInterface ) {
			if( fileJson.superclasses.last().contains( "Ext." ) )
				result = "extends ${ normalizeType( convertToInterface( fileJson.superclasses.last() ), true ) }"
		}
		else {
			if( getClassName( fileJson.name ) != fileJson.superclasses.last() )
				result = "extends ${ normalizeType( fileJson.superclasses.last(), true ) }"
		}
	}
	return result
}

def getImplementedInterfaces( fileJson ) {
	def result = "implements ${ normalizeType( convertToInterface( fileJson.name ), true ) }"
	def implementedInterfaces = []

	fileJson.mixins?.each { thisMixin ->
		implementedInterfaces.add( normalizeType( convertToInterface( thisMixin ), true ) )
	}

	if( implementedInterfaces.size() > 0 ) {
		result += ",${ implementedInterfaces.join( ',' ) }"
	}

	return result
}

task build( dependsOn: readFiles ) << {
	println " "
	println "Build complete"
	println " "
}


// HELPER METHODS

def getModule( name ) {
	def tokenizedName = name.tokenize( "." )
	if( tokenizedName.size() == 1 ) return name

	tokenizedName.pop()

	// Some package names absurdly duplicate class names like Ext.data.Store, so lowercase them...
	tokenizedName.eachWithIndex { thisName, i ->
		if( thisName != "Ext" ) tokenizedName[ i ] = thisName.toLowerCase()
	}
	return tokenizedName.join( "." )
}

def getClassName( name ) {
	def tokenizedName = name.tokenize( "." )
	def className = tokenizedName.last()
	return className
}

def getAliases( fileJson ) {
	return fileJson.aliases.widget
}

def writeProperties( fileJson, isInterface, useExport ) {
	def classConfig = fileJson.members.cfg
	def classProperties = fileJson.members.property
	def optionalFlag = isInterface ? "?" : ""
	def thisType
	def processedConfigNames = [:]
	def exportString = useExport ? "export var " : ""

	classConfig.each { value ->
		if( value.owner == fileJson.name ) {
			if( !includePrivate && value.private != true ) {

				// Don't output special cases where an item should be omitted due to incompatible ExtJS API overrides in subclasses
				if( !( specialCases[ fileJson.name ] && specialCases[ fileJson.name ][ value.name ] == "delete" ) ) {
					thisType = value.type

					// Property type conversions
					if( thisType.contains( "/" ) || thisType.contains( "|" ) ) thisType = "any"

					writeToDefinition( "\t\t/** [Config Option] (${value.type}) ${ formatCommentText( value.shortDoc ) } */" )
					writeToDefinition( "\t\t${ exportString }${ value.name.replaceAll( '-', '' ) }${ optionalFlag }: ${ normalizeType( convertToInterface( thisType ) ) };" )
				}

				processedConfigNames[ value.name ] = true
			}
		}
	}

	classProperties.each { value ->
		if( value.owner == fileJson.name ) {
			if( !includePrivate && value.private != true ) {
				if( !processedConfigNames[ value.name ] ) {
					thisType = value.type
					if( thisType.contains( "/" ) || thisType.contains( "|" ) ) thisType = "any"

					writeToDefinition( "\t\t/** [Property] (${value.type}) ${ formatCommentText( value.shortDoc ) } */" )
					writeToDefinition( "\t\t${ exportString }${ value.name.replaceAll( '-', '' ) }${ optionalFlag }: ${ normalizeType( convertToInterface( thisType ) ) };" )
					processedConfigNames[ value.name ] = true
				}
			}
		}
	}
	return processedConfigNames
}

def writeMethods( fileJson, processedNames, isInterface, useExport ) {
	def classMethods = fileJson.members.method
	def optionalFlag = isInterface ? "?" : ""
	def shouldUseExport = useExport
	def isSingleton = fileJson.singleton && !isInterface

	classMethods.each { value ->

		if( value.owner == fileJson.name || ( fileJson.mixins && value.owner in fileJson.mixins ) ) {
			if( !includePrivate && value.private != true ) {
				if( !processedNames[ value.name ] && !value?.meta?.deprecated ) {

					// Don't output special cases where an item should be omitted due to incompatible ExtJS API overrides in subclasses
					if( specialCases[ fileJson.name ] && specialCases[ fileJson.name ][ value.name ] == "toProperty" ) {
						writeToDefinition( "\t\t/** [Method] (${value.type}) ${ formatCommentText( value.shortDoc ) } */" )
						writeToDefinition( "\t\t${ value.name.replaceAll( '-', '' ) }${ optionalFlag }: any;" )
					}
					else {

						def tokenizedTypes = useFullTyping ? getTokenizedTypes( value.return.type ) : [ "any" ]

						tokenizedTypes.each { thisType ->

							// Return type conversions
							if( thisType == "undefined" ) thisType = "void"

							def paramNames = []
							def paramTypes = []
							def requiresOverrides = false

							value.params.each { thisParam ->
								paramNames.add( [ name:thisParam.name, optional:thisParam.optional, doc:thisParam.doc, default:thisParam.default ] )

								if( useFullTyping ) {
									def tokenizedParamTypes = getTokenizedTypes( thisParam.type )
									if( tokenizedParamTypes.size() > 1 && !requiresOverrides ) requiresOverrides = true
									paramTypes.add( tokenizedParamTypes )
								}
								else {
									paramTypes.add( thisParam.type )
								}
							}

							def usedPermutations = [:]

							if( ( requiresOverrides || useFullTyping ) && tokenizedTypes.first() == thisType ) {
								def overrideTypes = []
								paramNames.each { thisParamName ->
									overrideTypes.add( "any" )
								}
								writeMethod( value.shortDoc, value.name, optionalFlag, paramNames, overrideTypes, "any", shouldUseExport, isSingleton )
								usedPermutations[ overrideTypes.join( ',' ) ] = true
							}
							else if( !useFullTyping ) {
								writeMethod( value.shortDoc, value.name, optionalFlag, paramNames, paramTypes, thisType, shouldUseExport, isSingleton )
							}

							if( useFullTyping ) {
								def paramPermutations = GroovyCollections.combinations( paramTypes )

								paramPermutations.each { thisPermutation ->
									def thisPermutationAsString = thisPermutation.join( ',' )
									if( !usedPermutations[ thisPermutationAsString ] ) {
										writeMethod( value.shortDoc, value.name, optionalFlag, paramNames, thisPermutation, thisType, shouldUseExport, isSingleton )
										usedPermutations[ thisPermutationAsString ] = true
									}
								}
							}
						}
					}
				}
			}
		}
	}
}

def getTokenizedTypes( types ) {
	def result = types.replaceAll( "\\|", "/").tokenize( "/" )
	if( result.size() > 1 && types.contains( "Object" ) )
		result = ["any"]
	return result
}

def writeMethod( comment, methodName, optionalFlag, paramNames, paramTypes, returnType, useExport, isStatic=false ) {

	def exportString = useExport ? "export function " : ""
	def staticString = ( isStatic && !useExport && methodName != "constructor" ) ? "static " : ""
	comment = "\t\t/** [Method] ${ comment?.replaceAll( "[\\W]", " " ) }"

	def paramsDoc = ""
	def methodOutput = "${ staticString }${ methodName }${ optionalFlag }("

	paramNames.eachWithIndex { thisParam, i ->
		def thisParamType = convertToInterface( paramTypes[ i ] )

		paramsDoc += "\t\t* @param ${ thisParam.name } ${ thisParamType } ${ formatCommentText( thisParam.doc ) }"
		if( thisParam.doc && thisParam.doc.contains( "Optional " ) ) thisParam.optional = true

		//func(...args: any[]): void;
		def spread = ""
		if( thisParamType.startsWith( "..." ) ) {
			spread = "..."
		}

		def thisParamName = thisParam.name

		// Param name conversions
		if( thisParamName == "class" ) thisParamName = "clazz"

		def optionalParamFlag = thisParam.optional ? "?" : ""
		if( spread.size() == 0 && forceAllParamsToOptional ) optionalParamFlag = "?"
		methodOutput += " ${spread}${ thisParamName }${ optionalParamFlag }:${ spread.length() > 0 ? "any[]" : normalizeType( thisParamType ) }"

		if( thisParam == paramNames.last() ) {
			methodOutput += " "
		}
		else {
			methodOutput += ","
			paramsDoc += "\n"
		}
	}

	if( paramsDoc.length() > 0 ) {
		writeToDefinition( comment )
		writeToDefinition( paramsDoc )
		writeToDefinition( "\t\t*/" )
	}
	else {
		writeToDefinition( "${ comment }*/" )
	}

	if( methodName != "constructor" ) {
		writeToDefinition( "\t\t${exportString}${ methodOutput }): ${ normalizeType( returnType ) };" )
	}
	else {
		writeToDefinition( "\t\t${ methodOutput });" )
	}
}

def normalizeType( typeName, forceFullType=false ) {
	if( !useFullTyping && !forceFullType )
		return "any"

	if( typeName.contains( "Ext." ) )
		typeName = "${ getModule( typeName )}.${ getClassName( typeName ) }"

	if( typeName == "Mixed" ) typeName = "any"
	if( typeName == "TextNode" ) typeName = "any"
	if( typeName == "Arguments" ) typeName = "any"
	//if( typeName == "Object" ) typeName = "any"
	if( typeName == "String" ) typeName = "string"
	if( typeName == "Boolean" ) typeName = "bool"
	if( typeName == "Number" ) typeName = "number"

	return typeName.replaceAll( "\\.\\.\\.", "" )
}

def convertToInterface( type ) {
	def result = type
	if( type.contains( "Ext." ) ) {
		result = "${ getModule( type ) }.I${ getClassName( type ) }"
	}
	return result
}

def formatCommentText( comment ) {
	def result = ""
	if( comment ) result += comment.replaceAll("<(.|\n)*?>", "").replaceAll( "[\\n\\t]", " " )
	return result
}

def writeToDefinition( value ) {
	if( singleDefinition ) {
		definitionFile.withWriterAppend( "UTF-8" ) { writer ->
			writer << value + "\n"
		}
	}
	else {
		def currentDefinitionFile = new File( "${ definitionPath }/${ currentModule }.d.ts" )
		if( !currentDefinitionFile.exists() ) currentDefinitionFile.createNewFile()

		currentDefinitionFile.withWriterAppend( "UTF-8" ) { writer ->
			writer << value + "\n"
		}
	}
}


// TODO: Remove when not needed for testing any longer...

def iterateSections( fileJson ) {
	println( "Sections:" )
	fileJson.members.each { key, value ->
		println( key )
		//iterateSectionElements( value )
	}
	println( " " )
}

def iterateSection( section ) {
	section.each { value ->
		iterateSectionElements( value )
	}
	println( " " )
}

def iterateSectionElements( section ) {
	println( section.name )
	section.each { key, value ->
		//println( key )
	}
	println( " " )
}
